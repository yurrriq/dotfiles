\subsection{Config}

<<config/taskwarrior/default.nix>>=
{ config, lib, pkgs, ... }:

{

@

Install \hrefootnote{https://timewarrior.net}{Timewarrior}.

<<config/taskwarrior/default.nix>>=
  home.packages = with pkgs; [
    timewarrior
  ];

@


\newthought{Since home-manager generates a read-only \bash{.taskrc} file}, and
using \hrefootnote{https://taskwarrior.org/docs/context.html}{contexts} requires
Taskwarrior to be able to modify the it, use the following hacky workaround.

Set \bash{$TASKRC} to \bash{~/.taskrc-dirty}, which must contain at least
\ini{include ~/.config/task/taskrc}.

<<config/taskwarrior/default.nix>>=
  home.sessionVariables = {
    TASKRC = "~/.taskrc-dirty";
  };

@


\newthought{Define} short Taskwarrior shell aliases.

<<config/taskwarrior/default.nix>>=
  programs.fish.shellAliases = lib.mkIf (config.programs.fish.enable) rec {
    p = "task ls limit:page";
    po = "timew summary :week";
    pp = tbd;
    t = "task limit:page";
    ta = "task add";
    tbd = "task burndown.daily";
    te = "env VISUAL=$EDITOR task edit";
    tl = "task list";
    tm = "task mod";
    tw = "timew";
  };

@


\newthought{Configure Taskwarrior}, using
\hrefootnote{https://github.com/rycee/home-manager}{home-manager}.

<<config/taskwarrior/default.nix>>=
  programs.taskwarrior = {
    enable = true;
    colorTheme = "solarized-dark-256";
    config = {
      context.other = "-work";
      context.work = "+work";
    };
  };

@


\newthought{Link} the \nameref{subsec:on-exit-git-hook}.
\footnote{See next subssection.}

<<config/taskwarrior/default.nix>>=
  xdg.dataFile."task/hooks/on-exit-git.sh" = {
    executable = true;
    source = ./on-exit-git.sh;
  };

@

<<config/taskwarrior/default.nix>>=
  xdg.dataFile."task/hooks/on-modify.timewarrior" = {
    executable = true;
    source = let inherit (pkgs.timewarrior) version; in
      with pkgs; stdenv.mkDerivation {
        pname = "taskwarrior-on-modify.timewarrior";
        inherit version;
        nativeBuildInputs = [ makeWrapper ];
        buildInputs = [ python3 ];
        src = fetchurl {
          url = "https://raw.githubusercontent.com/GothenburgBitFactory/timewarrior/v${version}/ext/on-modify.timewarrior";
          sha512 = "sha512-GsDqetyfQOUU+VTZbgdKH1X6n5tM7q3Q0B5X/zk+JHgzw6vVk48IxGvCaDnpIJXCASIgSGsKLxLvv7RPDdlPAw==";
        };
        dontUnpack = true;
        installPhase = ''
          install -m755 $src $out
          substituteInPlace $out --replace "/usr/bin/env " ${python3}/bin/
        '';
      };
  };

}
@
