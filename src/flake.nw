The main entrypoint is
\href{https://github.com/yurrriq/dotfiles/blob/main/flake.nix}{flake.nix},
which defines a \hrefootnote{https://nixos.wiki/wiki/Flakes}{Nix Flake}.

At the top-level, the flake defines [[<<inputs>>]] and [[<<outputs>>]].

<<flake.nix>>=
{

  description = "My (semi-)literate, Nix-based dotfiles";

  inputs = {
    <<inputs>>
  };

  outputs = { self, ... }@inputs:
    let
      inherit (inputs.nixpkgs) lib;
      <<outputs variables>>
    in
    {
      <<outputs>>
    };

}
@

\section{Inputs}
\label{sec:inputs}

The \hrefootnote{https://github.com/nix-community/emacs-overlay}{Emacs overlay}
\say{comes with extra functions to generate an Emacs closure from various types
  of dependency declaration.} Notably, \nix{emacsWithPackagesFromUsePackage},
which generates an Emacs closure from an Emacs config file, by way of
\hrefootnote{https://jwiegley.github.io/use-package/}{\emacs{use-package}}.  See
\hyperref[sec:emacs]{the Emacs section} on page~\pageref{sec:emacs} for usage.

<<inputs>>=
emacs-overlay = {
  url = "github:nix-community/emacs-overlay";
  inputs.nixpkgs.follows = "nixpkgs";
};
@

\hrefootnote{https://github.com/nix-community/home-manager}{home-manager} is an
invaluable tool for managing a deterministic \bash{$HOME}.

<<inputs>>=
home-manager = {
  url = "github:nix-community/home-manager/release-21.05";
  inputs.nixpkgs.follows = "nixpkgs";
};
@

\href{https://github.com/yurrriq/naal}{naal} is a literate
\hrefootnote{https://core.tcl-lang.org/expect/index}{Expect} script I wrote,
for non-interactive
\href{https://github.com/sportradar/aws-azure-login}{AWS Azure Login}.

<<inputs>>=
naal = {
  url = "github:yurrriq/naal";
  inputs.nixpkgs.follows = "nixpkgs-unstable";
};
@

\hrefootnote{https://github.com/NixOS/nixos-hardware}{nixos-hardware} is
\say{a collection of NixOS modules covering hardware quirks.}

<<inputs>>=
nixos-hardware.url = "github:nixos/nixos-hardware";
@

Of course, pin a version of the
\hrefootnote{https://github.com/NixOS/nixpkgs}{Nix Packages collection}.

<<inputs>>=
nixpkgs.url = "github:nixos/nixpkgs/release-21.05";
nixpkgs-unstable.url = "github:nixos/nixpkgs";
@

The \hrefootnote{https://github.com/nix-community/NUR}{Nix User Repository}
provides \say{user contributed Nix packages.} Notably, I use some of
\href{https://rycee.net}{Robert Helgesson}'s
\hrefootnote{https://gitlab.com/rycee/nur-expressions/-/blob/master/pkgs/firefox-addons/default.nix}{Firefox
  add-ons} (see \hyperref[sec:firefox]{Firefox} on page~\pageref{sec:firefox}).

<<inputs>>=
nur.url = "github:nix-community/nur";
@

\section{Outputs}

\begin{minted}{nix}
  overlay = final: prev: {};
\end{minted}

<<outputs>>=
overlays = {
  <<Overlays>>
};

devShell.x86_64-linux = pkgs.callPackage ./shell.nix {
  inherit pkgs;
  yurrriq-dotfiles = self.defaultPackage.x86_64-linux;
};

packages.x86_64-linux = {
  fish-kubectl-completions = pkgs.callPackage ./pkgs/shells/fish/kubectl-completions { };
  yurrriq-dotfiles = pkgs.callPackage ./default.nix { inherit pkgs; };
};

defaultPackage.x86_64-linux = self.packages.x86_64-linux.yurrriq-dotfiles;

nixosModules = {
  <<Modules>>
};

nixosConfigurations = {
  "nixps" = mkSystem "nixps" "dell-xps-15-9560-intel";
  "MSP-EBAILEY01" = mkSystem "sruxps" "dell-xps-13-7390";
};
@

\subsection{Variables}

\begin{minted}{haskell}
  mkSystem :: String -> String -> AttrSet
\end{minted}

<<outputs variables>>=
mkSystem = name: machine: lib.nixosSystem {
  modules = [
    (./machines + "/${name}/hardware-configuration.nix")
    inputs.nixos-hardware.nixosModules.${machine}
    inputs.nixos-hardware.nixosModules.common-pc-laptop-ssd
    inputs.home-manager.nixosModules.home-manager
    {
      home-manager = {
        useGlobalPkgs = true;
        useUserPackages = true;
        verbose = true;
      };
    }
    self.nixosModules.common
    self.nixosModules.location
    self.nixosModules.nix
    self.nixosModules.nixPath
    self.nixosModules.nixRegistry
    self.nixosModules.nixos
    self.nixosModules.nixpkgs
    self.nixosModules.bootyjams
    self.nixosModules.clis
    self.nixosModules.applications
    self.nixosModules.virtualisation
    (./machines + "/${name}/configuration.nix")
  ];
  system = "x86_64-linux";
};
@

<<outputs variables>>=
pkgNameElem = names: pkg:
  builtins.elem
    (builtins.parseDrvName
      (if builtins.hasAttr "pname" pkg then pkg.pname
      else if builtins.hasAttr "name" pkg then pkg.name
      else "")).name
    names;
@

<<outputs variables>>=
pkgs = import inputs.nixpkgs {
  overlays = [
    self.overlays.noweb
    self.overlays.unstable
  ];
  system = "x86_64-linux";
};
@

<<outputs variables>>=
unstable-pkgs = import inputs.nixpkgs-unstable {
  config.allowUnfreePredicate = pkgNameElem [
    "zoom"
  ];
  system = "x86_64-linux";
};
@

\subsection{Overlays}

<<Overlays>>=
fish-completions = final: prev: {
  fish-kubectl-completions = prev.callPackage ./pkgs/shells/fish/kubectl-completions { };
};
@

<<Overlays>>=
nodePackages = final: prev: {
  nodePackages =
    unstable-pkgs.nodePackages // prev.callPackage ./pkgs/development/node-packages {
      inherit (prev) pkgs nodejs;
    };
};
@

<<Overlays>>=
noweb = final: prev: {
  noweb = unstable-pkgs.noweb.override {
    icon-lang = unstable-pkgs.icon-lang.override {
      withGraphics = false;
    };
  };
};
@

<<Overlays>>=
unstable = final: prev: {
  inherit (unstable-pkgs)
    autojump
    browserpass
    conftest
    delta
    eksctl
    kubectx
    kubelogin
    pass
    renderizer
    ripgrep
    scc
    signal-desktop
    sops
    starship
    super-productivity
    tomb
    yq
    zoom-us
    zoxide
    ;
  inherit (unstable-pkgs.python3Packages)
    bugwarrior
    ec2instanceconnectcli
    ;
};
@

\subsection{NixOS Modules}

<<Modules>>=
applications = import ./modules/applications.nix;

bootyjams = import ./modules/bootyjams.nix;

clis = import ./modules/clis.nix;

common = import ./modules/common.nix;

location = import ./modules/location.nix;

nix = import ./modules/nix.nix;

nixPath = {
  nix.nixPath = lib.mapAttrsToList (n: v: "${n}=${v}")
    (lib.filterAttrs (n: _: n != "self") inputs);
};

nixRegistry = {
  nix.registry = {
    home-manager.flake = inputs.home-manager;
    nixpkgs.flake = inputs.nixpkgs;
    nixpkgs-unstable.flake = inputs.nixpkgs-unstable;
    nur.flake = inputs.nur;
  };
};

nixos = import ./modules/nixos.nix;

nixpkgs = {
  nixpkgs.config.allowUnfreePredicate = pkgNameElem [
    "lastpass-password-manager"
    "reaper"
    "slack"
    "spotify"
    "spotify-unwrapped"
    "steam"
    "steam-original"
    "steam-runtime"
  ];
  nixpkgs.overlays = lib.attrValues self.overlays ++ [
    inputs.emacs-overlay.overlay
    inputs.naal.overlays.naal
    inputs.nur.overlay
  ];
};

virtualisation = import ./modules/virtualisation.nix;
@


%% Local Variables:
%% noweb-code-mode: nix-mode
%% End:
